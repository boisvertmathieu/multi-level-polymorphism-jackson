plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.openapi.generator' version '7.5.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
java {
    sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

openApiGenerate {
    generatorName = 'java'
    library = 'webclient'
    inputSpec = "$rootDir/src/main/resources/openapi/external-api.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = 'com.example.generated.api'
    modelPackage = 'com.example.generated.model'
    invokerPackage = 'com.example.generated.invoker'
    configOptions = [
            dateLibrary: 'java8',
            useJakartaEe: 'true',
            serializableModel: 'true',
            useTags: 'true',
            hideGenerationTimestamp: 'true'
    ]
    globalProperties = [
            apis      : 'false',
            models    : '',
            supportingFiles: 'false'
    ]
    generateModelTests = false
    generateModelDocumentation = false
    generateApiTests = false
    generateApiDocumentation = false
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

compileJava {
    dependsOn tasks.openApiGenerate
}

clean {
    delete "$buildDir/generated"
}

bootRun {
    dependsOn tasks.openApiGenerate
}

